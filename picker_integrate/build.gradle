plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-parcelize'
}

apply from: "../config.gradle"

android {
    compileSdk project.ext.compile_sdk

    namespace 'com.liabit.integratepicker'

    defaultConfig {
        minSdk 21
        targetSdk project.ext.target_sdk

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
    }

    libraryVariants.all { variant ->
        variant.assemble.doLast {
            variant.outputs.all { output ->
                if (output.outputFileName.contains("debug")) {
                    // output project-directory/build/outputs/aar/debug
                    def outputPath = [project.rootDir.absolutePath, "build", "outputs", "aar", "debug"].join(File.separator)
                    copy {
                        from output.outputFile
                        into outputPath
                    }
                } else {
                    // output project-directory/build/outputs/aar/release
                    def outputPath = [project.rootDir.absolutePath, "build", "outputs", "aar", "release"].join(File.separator)
                    copy {
                        from output.outputFile
                        into outputPath
                    }
                }
            }
        }
    }
}

dependencies {
    implementation "androidx.appcompat:appcompat:${project.androidx_appcompat__appcompat}"

    compileOnly project(':ext')
    compileOnly project(':citypicker')
    compileOnly project(':picker')
    compileOnly project(':photoview')
    compileOnly project(':photopicker')
    compileOnly project(':numberpicker')
    compileOnly project(':matisse')

    testImplementation "junit:junit:${project.junit__junit}"
    androidTestImplementation "androidx.test.ext:junit:${project.androidx_test_ext__junit}"
    androidTestImplementation "androidx.test.espresso:espresso-core:${project.androidx_test_espresso__espresso_core}"
}

if (file('binary-config.gradle').exists() && file('../binary-common.gradle').exists()) {
    apply from: '../binary-common.gradle'
}